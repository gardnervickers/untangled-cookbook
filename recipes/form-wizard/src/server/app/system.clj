(ns app.system
  (:require
    [untangled.server.core :as core]
    [app.api :as api]
    [om.next.server :as om]
    [taoensso.timbre :as timbre]
    [com.stuartsierra.component :as comp]
    [clojure.java.jdbc :as jdbc]
    [om.next.impl.parser :as op])
  (:import (clojure.lang IFn)))

(defn logging-mutate [env k params]
  (timbre/info "Mutation Request: " k)
  (api/apimutate env k params))

(defn logging-query [{:keys [ast] :as env} k params]
  (timbre/info "Query: " (op/ast->expr ast))
  (api/api-read env k params))

(defn build-schema [db]
  (db (fn [c]
        (jdbc/execute! c [(str "CREATE TABLE IF NOT EXISTS facial_survey ("
                            "id integer generated by default as identity primary key,"
                            "gender varchar(10),"
                            "like_shaving boolean,"
                            "beards_sexy boolean)")]))))

(defrecord Database [dbspec run-in-transaction]
  IFn
  (invoke [this operation]
    (jdbc/with-db-connection [conn dbspec]
      (jdbc/with-db-transaction [c conn]
        (operation c))))
  comp/Lifecycle
  (start [this]
    (let [db {:dbtype         "hsqldb"
              :connection-uri "jdbc:hsqldb:mem:resultsdb"
              :user           "SA"
              :password       ""}
          rv (assoc this :dbspec db)]
      (build-schema rv)
      rv))
  (stop [this] this))

(defn make-system []
  (core/make-untangled-server
    :config-path "config/recipe.edn"
    :parser (om/parser {:read logging-query :mutate logging-mutate})
    :parser-injections #{:database}
    :components {:database (map->Database {})}))
